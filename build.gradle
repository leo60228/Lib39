buildscript {
	repositories {
		mavenCentral()
		gradlePluginPortal()
	}
	dependencies {
		classpath 'com.google.code.gson:gson:2.8.7'
		classpath 'com.modrinth.minotaur:Minotaur:2.4.3'
		classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.4.0'
	}
}
plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT'
	id 'io.github.juuxel.loom-quiltflower' version '1.7.3'
	id 'org.quiltmc.quilt-mappings-on-loom' version '4.2.0'
	id 'maven-publish'
}

if (System.getenv("MODRINTH_TOKEN")) {
	apply plugin: "com.modrinth.minotaur"
}
if (System.getenv("CURSE_TOKEN")) {
	apply plugin: "com.matthewprenger.cursegradle"
}

sourceCompatibility = JavaVersion.toVersion(17)
targetCompatibility = JavaVersion.toVersion(17)

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven {
		url 'https://repo.unascribed.com'
		content {
			includeGroup 'com.unascribed'
		}
	}
	maven {
		url 'https://maven.quiltmc.org/repository/release'
		content {
			includeGroup 'org.quiltmc'
		}
	}
	maven {
		url 'https://maven.terraformersmc.com/releases/'
		content {
			includeGroup 'dev.emi'
			includeGroup 'com.terraformersmc'
		}
	}
	maven {
		url "https://maven.shedaniel.me/"
		content {
			includeGroup 'me.shedaniel'
			includeGroup 'me.shedaniel.cloth'
			includeGroup 'me.shedaniel.cloth.api'
			includeGroup 'dev.architectury'
		}
	}
	maven {
		url "https://dvs1.progwml6.com/files/maven/"
		content {
			includeGroup 'mezz.jei'
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings(loom.layered {
		addLayer quiltMappings.mappings("org.quiltmc:quilt-mappings:${minecraft_version}+build.${mappings_version}:v2")
	})
	modImplementation "net.fabricmc:fabric-loader:${loader_version}"

	modCompileOnly "dev.emi:emi:0.3.1+1.19"
	modCompileOnly "me.shedaniel:RoughlyEnoughItems-api:9.1.520"
	// the API jar doesn't get remapped by Loom because it doesn't have a fabric.mod.json
	modCompileOnly("mezz.jei:jei-1.19.1-fabric:11.2.0.244") { transitive = false }

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
}

loom {
	accessWidenerPath = file("src/main/resources/lib39.accesswidener")
}

java {
	withSourcesJar()
}

tasks.validateAccessWidener {
	enabled = false
}

processResources {
	inputs.property "version", project.version

	filesMatching("**/fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.jar {
	destinationDirectory.set(file('build/tmp'))
	classifier 'all'
}

tasks.remapJar {
	destinationDirectory.set(file('build/tmp'))
	classifier 'all-remapped'
}

tasks.remapSourcesJar {
	destinationDirectory.set(file('build/tmp'))
	classifier 'all-sources-remapped'
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"
}

tasks.withType(AbstractArchiveTask) {
	preserveFileTimestamps = false
	reproducibleFileOrder = true
}

def amender = evaluate(file("JarAmender.groovy"))

task splitRefmap {
	dependsOn remapJar
	doFirst {
		amender(remapJar.archivePath)
	}
}

import com.google.gson.GsonBuilder;
import com.google.gson.JsonObject;
import com.google.gson.JsonArray;

def gson = new GsonBuilder().setPrettyPrinting().create()

def fatJarFmj = new JsonObject()
fatJarFmj.addProperty("schemaVersion", 1)
fatJarFmj.addProperty("id", "lib39")
fatJarFmj.addProperty("name", "Lib39")
fatJarFmj.addProperty("version", project.version)
fatJarFmj.addProperty("description", "A library of useful tidbits originating in Yttr.")
fatJarFmj.addProperty("icon", "lib39.png")
fatJarFmj.addProperty("license", "MIT")
fatJarFmj.addProperty("environment", "*")
def fatJarJars = new JsonArray()
fatJarFmj.add("jars", fatJarJars)


task fatJar(type: Jar) {
	doFirst {
		file('build/tmp/fatjar').mkdirs()
		file('build/tmp/fatjar/fabric.mod.json').text = gson.toJson(fatJarFmj)
	}
	classifier ''
	from 'src/main/resources/lib39.png'
	from 'build/tmp/fatjar/fabric.mod.json'
}

def uploadAdditionalFiles = []

tasks.remapJar.finalizedBy splitRefmap

def defineModule = { name, depModules ->
	def Name = name.substring(0, 1).toUpperCase(Locale.ROOT)+name.substring(1)
	def t = tasks.create(name+'Jar', Jar) {
		dependsOn remapJar
		classifier name
		from(zipTree(remapJar.archivePath)) {
			include 'META-INF/**'
			include 'com/unascribed/lib39/'+name+'/**'
			include name+'/**'
			include 'lib39.png'
			includeEmptyDirs = false
			eachFile {
				if (it.path.startsWith(name+"/")) {
					it.path = it.path.substring(name.length()+1)
				}
			}
		}
	}
	tasks.build.dependsOn t
	t.finalizedBy(tasks.create('amend'+Name+'Jar') {
		dependsOn t
		doFirst {
			amender(t.archivePath)
		}
	})

	tasks.fatJar.dependsOn(t)
	tasks.fatJar.from(t.archivePath) {
		into 'META-INF/jars'
	}
	uploadAdditionalFiles.add(t)
	def jarObj = new JsonObject()
	jarObj.addProperty("file", "META-INF/jars/"+t.archiveFileName.get())
	fatJarJars.add(jarObj)

	def srcT = tasks.create(name+'SourcesJar', Jar) {
		dependsOn remapSourcesJar
		classifier name+'-sources'
		from(zipTree(remapSourcesJar.archivePath)) {
			include 'META-INF/**'
			include 'com/unascribed/lib39/'+name+'/**'
			include name+'/**'
			include 'lib39.png'
			includeEmptyDirs = false
			eachFile {
				if (it.path.startsWith(name+"/")) {
					it.path = it.path.substring(name.length()+1)
				}
			}
		}
	}

	loom.disableDeprecatedPomGeneration(publishing.publications.create(name, MavenPublication) {
		groupId 'com.unascribed'
		artifactId 'lib39-'+name
		version project.version
		artifact(t.archivePath) {
			classifier ''
			builtBy t
		}
		artifact(srcT.archivePath) {
			classifier 'sources'
			builtBy srcT
		}

		pom.withXml { xml ->
			def deps = xml.asNode().appendNode('dependencies')
			depModules.each {
				def dep = deps.appendNode('dependency')
				def spec = ['com.unascribed', 'lib39-'+it, project.version]
				if (it.contains(':')) {
					spec = it.split(":")
				}
				if (spec[0] == 'fapi') {
					spec[0] = 'net.fabricmc.fabric-api'
					spec[2] = fabricApi.moduleVersion(spec[1], spec[2])
				}
				dep.appendNode('groupId', spec[0])
				dep.appendNode('artifactId', spec[1])
				dep.appendNode('version', spec[2])
			}
		}
	})
}

defineModule 'core', []
defineModule 'lockpick', ['core']
defineModule 'gesundheit', ['core']
defineModule 'fractal', ['core', "fapi:fabric-resource-loader-v0:${fabric_version}"]
defineModule 'tunnel', ['core']
defineModule 'phantom', ['core']
defineModule 'waypoint', ['core',
	"fapi:fabric-transitive-access-wideners-v1:${fabric_version}",
	"fapi:fabric-lifecycle-events-v1:${fabric_version}",
	"fapi:fabric-rendering-v1:${fabric_version}",
	"fapi:fabric-resource-loader-v0:${fabric_version}"
]
defineModule 'weld', ['core', "fapi:fabric-resource-loader-v0:${fabric_version}"]
defineModule 'sandman', ['core', "fapi:fabric-lifecycle-events-v1:${fabric_version}"]
defineModule 'crowbar', ['core', "fapi:fabric-api-base:${fabric_version}"]
defineModule 'keygen', ['core']
defineModule 'dessicant', ['core']
defineModule 'deferral', []
defineModule 'machination', ['core',
	"fapi:fabric-lifecycle-events-v1:${fabric_version}",
	"fapi:fabric-resource-loader-v0:${fabric_version}"
]
defineModule 'util', ['core']
defineModule 'ripple', ['core']
defineModule 'recoil', ['core', "fapi:fabric-api-base:${fabric_version}", "fapi:fabric-networking-api-v1:${fabric_version}"]
defineModule 'mesh', ['core', "fapi:fabric-lifecycle-events-v1:${fabric_version}"]

if (System.getenv("MODRINTH_TOKEN")) {
	modrinth {
		token = System.getenv("MODRINTH_TOKEN")
		projectId = 'lib39'
		versionNumber = project.version
		versionName = 'v'+project.version
		versionType = 'release'
		uploadFile = fatJar
		additionalFiles = uploadAdditionalFiles
		gameVersions = ['1.19.1', '1.19.2']
		loaders = ['fabric', 'quilt']
		detectLoaders = false
	}
}

if (System.getenv("CURSE_TOKEN")) {
	curseforge {
		apiKey = System.getenv("CURSE_TOKEN")
		project {
			id = '661690'
			releaseType = 'release'

			addGameVersion '1.19.1'
			addGameVersion '1.19.2'
			addGameVersion 'Fabric'
			addGameVersion 'Quilt'

			mainArtifact(fatJar) {
				displayName = 'v'+project.version
			}

			uploadAdditionalFiles.reverseEach { task ->
				addArtifact(task) {
					displayName = 'Single Module: '+task.classifier.substring(0, 1).toUpperCase()+task.classifier.substring(1)
				}
			}
		}
		options {
			javaIntegration = false
			forgeGradleIntegration = false
			javaVersionAutoDetect = false
		}
	}
}

publishing {
	repositories {
		if (project.hasProperty("publish-username")) {
			maven {
				url "https://repo-api.sleeping.town/"
				credentials {
					username project.getProperty("publish-username")
					password project.getProperty("publish-password")
				}
			}
		}
		maven {
			url file('build/maven').toURI().toString()
		}
	}
}
